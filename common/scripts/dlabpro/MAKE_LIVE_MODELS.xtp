#!/usr/bin/env dlabpro
## UASR: Unified Approach to Speech Synthesis and Recognition
## - Creates recognizer models for LIVE demo from scratch 
## AUTHOR : Matthias Wolff
##
## PACKAGE: uasr-data/coins/common/scripts/dlabpro

"$UASR_HOME/scripts/dlabpro/util/os.itp"  include;
"$UASR_HOME/scripts/dlabpro/util/var.itp" include;

function -MLM_find_best_model()
{
  data idList;   "log" "hmm-*_assess.dn3" FALSE idList -SH_find;                # Load assessment logs
  data idAssess;                                                                # Assessment log
  data idAux;                                                                   # Auxiliary data
  var i;                                                                        # Loop counter
  var j;                                                                        # Loop counter
  var cor;                                                                      # Correctness
  var maxCor;     0 maxCor =;                                                   # Best correctness
  var sAm         "0_0" sAm =;                                                  # Acoustic model ID

  "\n\n   Analyzing $[idList.nrec] HMM assessment logs ..."              -echo;
  idList "?hmm-?" "replace" idAux -strop;
  idAux "?_assess.dn3" "replace" idAux -strop;
  idAux idList -join;
  0 i =; i idList.nrec < while
    "\n   - HMM ${idList[i,1]}: "                                        -echo;
    "log/${idList[i,0]}" idAssess -restore;
    0 j =; j idAssess.nrec < while
      ( :idAssess[j,"RES"]: :idAssess[j,"REF"]: == ) cor +=;
      j ++=;
    end
    :cor/=idAssess.nrec;
    "${round(cor*1000)/10} % correct" -echo;
    cor maxCor > if cor maxCor =; :idList[i,1]: sAm =; end
    i ++=;
  end
  "\n   Best model: $[sAm]\n\n Confusion matrix:"                        -echo;
  "log/hmm-$[sAm]_assess-cmx.dn3" idAux -restore; idAux -print;
  
  sAm return;
}

function -MLM_gen_recpackdata_cfg(sAm)
{
  DGen iDG;
  var  sExpDir; "\$UASR_HOME-data/coins/LIVE" sExpDir =;

  "\n\n   Generating info/REC_PACKDATA.cfg ..."                          -echo;
  "\#\# UASR configuration file\n"                                      iDG >>;
  "\#\# - Coins database - live demo: Configuration of "
    "REC_PACKDATA.xtp\n" +                                              iDG >>;
  "\#\#   NOTE: This file was generated by $__SFILE__.xtp\n"            iDG >>;
  "\#\#\n"                                                              iDG >>;
  "\#\#   Usage:\n"                                                     iDG >>;
  "\#\#\n"                                                              iDG >>;
  "\#\#     dlabpro \$UASR_HOME/scripts/dlabpro/tools/REC_PACKDATA.xtp "
    "rec REC_PACKDATA.cfg\n" +                                          iDG >>;
  "\n"                                                                  iDG >>;
  "\#\# UASR keys\n"                                                    iDG >>;
  "uasr.db       = \"coins\"\n"                                         iDG >>;
  "uasr.exp      = \"LIVE\"\n"                                          iDG >>;
  "uasr.am.model = \"$[sAm]\"\n"                                        iDG >>;
  "uasr.am.sil   = 8;\n"                                                iDG >>;
  "uasr.am.gbg   = -1;\n"                                               iDG >>;
  "\n"                                                                  iDG >>;
  "\#\# REC_PACKDATA keys\n"                                            iDG >>;
  "uasr.out      = \"" sExpDir + "/model\"\n" +                         iDG >>;
  "uasr.lm.fsg   = \"" sExpDir + "/info/recognizer.grm.txt\"\n" +       iDG >>; 
  "uasr.render   = \"yes\"\n"                                           iDG >>;
  "\n"                                                                  iDG >>;
  "\#\# EOF"                                                            iDG >>;
  "info/REC_PACKDATA.cfg" iDG -write; " done."                           -echo;
}

function -MLM_gen_recognizer_cfg(sAm)
{
  DGen iDG;
  var  sExpDir; "\$UASR_HOME-data/coins/LIVE" sExpDir =;

  "\n   Generating info/recognizer.cfg ..."                              -echo;
  "\#\# UASR configuration file\n"                                      iDG >>;
  "\#\# - Coins database - live demo: Configuration for recognizer\n"   iDG >>;
  "\#\#   NOTE: This file was generated by $__SFILE__.xtp\n"            iDG >>;
  "\#\#\n"                                                              iDG >>;
  "\#\#   Usage:\n"                                                     iDG >>;
  "\#\#\n"                                                              iDG >>;
  "\#\#     recognizer -cfg recognizer.cfg mysoundfile.wav\n"           iDG >>;
  "\n"                                                                  iDG >>;
  "sig.sample_rate = 48000\n"                                           iDG >>;
  "data.dialog     =\n"                                                 iDG >>;
  "data.feainfo    = " sExpDir + "/model/feainfo.object\n" +            iDG >>;
  "data.gmm        = " sExpDir + "/model/$[sAm].gmm\n" +                iDG >>;
  "data.vadinfo    = " sExpDir + "/model/3_10_mod.vad\n" +              iDG >>;
  "data.sesinfo    = " sExpDir + "/model/sesinfo.object\n" +            iDG >>;
  "vad.nolimit     = yes\n"                                             iDG >>;
  "out             = cmd\n"                                             iDG >>;
  "\n"                                                                  iDG >>;
  "\#\# EOF"                                                            iDG >>;
  "info/recognizer.cfg" iDG -write; " done."                             -echo;
}

## == MAIN ==

var sCmd;
var sAm;  "0_0" sAm =;

"$UASR_HOME-data/coins/LIVE" -cd;

## Feature analysis
#"dlabpro $UASR_HOME/scripts/dlabpro/FEA.xtp ana info/HMM-trn.cfg" sCmd =;
#"\n\nINVOKE: " sCmd + "\n" + -echo; sCmd -system;

## HMM training
#"dlabpro $UASR_HOME/scripts/dlabpro/HMM.xtp trn info/HMM-trn.cfg" sCmd =;
#"\n\nINVOKE: " sCmd + "\n" + -echo; sCmd -system;

## Create configuration files
( -MLM_find_best_model ) sAm =;
sAm -MLM_gen_recpackdata_cfg;
sAm -MLM_gen_recognizer_cfg;

## Pack recognizer data
"dlabpro $UASR_HOME/scripts/dlabpro/tools/REC_PACKDATA.xtp rec info/REC_PACKDATA.cfg" sCmd =;
"\n\nINVOKE: " sCmd + "\n" + -echo; sCmd -system;

## Test recognizer
"\n\n   Creating flists/test_wav.flst ..."                               -echo;
data idFlist;
data idFiles;
data idLab;
data idAux;
"flists/test.flst" "ascii" idFlist stdfile -import;
idFlist 0 1 idFiles -select;
idFlist 1 1 idLab -select;
{ "$UASR_HOME-data/coins/common/sig/" } idFlist.nrec idFlist -resample;
idFlist "?\\?/" "replace" idFlist -strop;
idFiles idFlist -join;
{ ".wav" }  idFlist.nrec idAux -resample;
idAux idFlist -join;
idFlist "" "ccat" idFlist -strop;
idLab idFlist -join;
"flists/test_wav.flst" "ascii" idFlist stdfile -export;
" done."                                                                 -echo;

"\n\n   Testing recognizer ..."                                          -echo;
"recognizer -cfg info/recognizer.cfg flists/test_wav.flst" sCmd =;
"\n\nINVOKE: " sCmd + "\n" + -echo; sCmd -system;

quit;
## EOF